{
  description = "Desktop window and tray for the local mihomo-server instance";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    # The service: its packaged helper starts and stops the declared instance.
    mihomo-server = {
      url = "github:oodzchen/mihomo-server";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "flake-utils";
    };
  };

  outputs = { self, nixpkgs, flake-utils, mihomo-server }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = nixpkgs.legacyPackages.${system};
        lib = pkgs.lib;

        desktopRelease = {
          version = "0.2.20";
          hash = "sha256-XWaWA26AmO5EKMA2IhjXh1OF+4ua4eZK6TyGr9HPL2U=";
        };
        # Releases up to v0.2.20 were published by the service repository.
        releaseRepository =
          if lib.versionOlder desktopRelease.version "0.2.21"
          then "oodzchen/mihomo-server"
          else "oodzchen/mihomo-server-desktop";

        serverHelper =
          if system == "x86_64-linux"
          then "${mihomo-server.packages.${system}.server-bin}/mihomo-server-user"
          else "mihomo-server-user";

        desktopIntegration = ''
            echo '{"kind":"nix"}' > $out/nix-installation.json
            cat > $out/nix-install-service <<'EOF'
            #!${pkgs.runtimeShell}
            echo 'Enable services.mihomo-server and select services.mihomo-server.users in NixOS, then run nixos-rebuild switch.' >&2
            exit 1
            EOF
            chmod +x $out/nix-install-service
            cat > $out/nix-service-helper <<'EOF'
            #!${pkgs.runtimeShell}
            if [ "''${1:-}" = enable ]; then shift; set -- start "$@"; fi
            exec ${serverHelper} "$@"
            EOF
            chmod +x $out/nix-service-helper
            # Use one wrapper: nested shell wrappers change GTK's argv[0]
            # to .mihomo-server-desktop-wrapped_, breaking Wayland icon lookup.
            wrapProgram $out/bin/mihomo-server-desktop \
              --argv0 mihomo-server-desktop \
              "''${gappsWrapperArgs[@]}" \
              --set MIHOMO_DESKTOP_LAUNCHER mihomo-server-desktop \
              --suffix PATH : $out/bin \
              --set MIHOMO_SERVER_HELPER $out/nix-service-helper \
              --set MIHOMO_SERVER_INSTALLER $out/nix-install-service
          '';

        runtimeLibraries = with pkgs; [
          gtk3
          webkitgtk_4_1
          glib
          glib-networking
          openssl
          libayatana-appindicator
          librsvg
          xdotool
        ];

        desktop-source = pkgs.rustPlatform.buildRustPackage {
          pname = "mihomo-server-desktop";
          version = desktopRelease.version;
          src = ./.;
          MIHOMO_DESKTOP_VERSION = desktopRelease.version;

          cargoLock = {
            lockFile = ./Cargo.lock;
            # management-client comes from a pinned mihomo-server tag.
            allowBuiltinFetchGit = true;
          };

          nativeBuildInputs = with pkgs; [
            pkg-config
            wrapGAppsHook3
            makeWrapper
          ];

          buildInputs = runtimeLibraries;

          dontWrapGApps = true;
          postFixup = desktopIntegration;

          postInstall = ''
            install -Dm644 mihomo-server-desktop.desktop $out/share/applications/mihomo-server-desktop.desktop
            substituteInPlace $out/share/applications/mihomo-server-desktop.desktop \
              --replace-fail '{{exec}}' 'mihomo-server-desktop' \
              --replace-fail '{{icon}}' 'mihomo-server-desktop'

            install -Dm644 icons/src/app.svg $out/share/icons/hicolor/scalable/apps/mihomo-server-desktop.svg
            install -Dm644 icons/icon.png $out/share/icons/hicolor/512x512/apps/mihomo-server-desktop.png
            install -Dm644 icons/128x128@2x.png $out/share/icons/hicolor/256x256/apps/mihomo-server-desktop.png
            install -Dm644 icons/128x128.png $out/share/icons/hicolor/128x128/apps/mihomo-server-desktop.png
            install -Dm644 icons/32x32.png $out/share/icons/hicolor/32x32/apps/mihomo-server-desktop.png
            install -Dm644 icons/icon.png $out/share/pixmaps/mihomo-server-desktop.png
          '';
        };

        desktop-bin = pkgs.stdenv.mkDerivation rec {
          pname = "mihomo-server-desktop";
          version = desktopRelease.version;

          src = pkgs.fetchurl {
            url = "https://github.com/${releaseRepository}/releases/download/v${version}/mihomo-server-desktop-v${version}-x86_64.tar.gz";
            hash = desktopRelease.hash;
          };

          nativeBuildInputs = with pkgs; [
            autoPatchelfHook
            wrapGAppsHook3
            makeWrapper
          ];

          buildInputs = runtimeLibraries;

          dontWrapGApps = true;
          postFixup = desktopIntegration;

          installPhase = ''
            runHook preInstall
            mkdir -p $out
            cp -r bin share $out/
            runHook postInstall
          '';
        };
      in
      {
        packages = {
          default = if system == "x86_64-linux" then desktop-bin else desktop-source;
          desktop = self.packages.${system}.default;
          desktop-bin = desktop-bin;
          desktop-source = desktop-source;
        };

        checks = lib.optionalAttrs (system == "x86_64-linux") {
          module = import ./nix/tests.nix { inherit self nixpkgs pkgs mihomo-server; };
        };

        devShells.default = pkgs.mkShell {
          buildInputs = with pkgs; [
            cargo
            rustc
            rustfmt
            clippy
            rustup
            nodejs
            python3
            pkg-config
          ] ++ runtimeLibraries;
        };
      }
    ) // {
      nixosModules.default = import ./nix/module.nix { inherit self; };
    };
}
